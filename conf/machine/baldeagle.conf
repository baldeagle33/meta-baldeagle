#@TYPE: Machine
#@NAME: baldeagle

#@DESCRIPTION: Machine configuration for baldeagle systems

PREFERRED_PROVIDER_virtual/kernel ?= "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "linux-yocto-rt", "linux-yocto", d)}"
PREFERRED_VERSION_linux-yocto ?= "4.14%"

# Tuning
require conf/machine/include/tune-baldeagle.inc

include conf/machine/include/amd-common-configurations.inc
include conf/machine/include/amd-customer-configurations.inc

# Disable GPU if RT kernel is in use
XSERVER_X86_AMDGPU = " \
           xf86-video-amd \
           ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-radeonsi', '', d)} \
           "
XSERVER_X86_NOGPU = "${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast', '', d)}"
XSERVER_X86_GPU = "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "${XSERVER_X86_NOGPU}", "${XSERVER_X86_AMDGPU}", d)}"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_GPU} \
           "

MACHINE_EXTRA_RRECOMMENDS += "radeon-firmware grub-efi"
MACHINE_EXTRA_RRECOMMENDS_remove = "grub gpio-test spi-test"

APPEND += "radeon.dpm=1"

MACHINEOVERRIDES =. "amd:amdx86:radeon:"
