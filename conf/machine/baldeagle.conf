#@TYPE: Machine
#@NAME: baldeagle

#@DESCRIPTION: Machine configuration for baldeagle systems

PREFERRED_PROVIDER_virtual/kernel_baldeagle ?= "linux-yocto"
PREFERRED_VERSION_linux-yocto_baldeagle ?= "4.9%"
PREFERRED_VERSION_mesa ?= "17.3.%"

# Tuning
require conf/machine/include/tune-baldeagle.inc
#require conf/machine/include/tune-amd-baldeagle.inc

include conf/machine/include/amd-common-configurations.inc
include conf/machine/include/amd-customer-configurations.inc

# Add graphics features (if required)
DISTRO_FEATURES_append = "${@bb.utils.contains('GRAPHICS_IMAGE', '1', ' x11 opengl', '', d)}"

# Disable GPU if RT kernel is in use
XSERVER_X86_RADEON = "xf86-video-amd \
           ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-radeon', '', d)} \
           "
XSERVER_X86_NOGPU = "${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast', '', d)}"
XSERVER_X86_GPU = "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "${XSERVER_X86_NOGPU}", "${XSERVER_X86_RADEON}", d)}"

XSERVER_baldeagle ?= " \
            ${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_GPU} \
            "

MACHINE_EXTRA_RRECOMMENDS += "grub-efi"
MACHINE_EXTRA_RRECOMMENDS += "radeon-firmware"
MACHINE_EXTRA_RRECOMMENDS += "vdpauinfo parted util-linux-blkid"
#MACHINE_EXTRA_RRECOMMENDS += "amd-gpio"
MACHINE_EXTRA_RRECOMMENDS_remove = "grub gpio-test spi-test"

# Make sure firmware and kernel modules get included in any image
IMAGE_INSTALL_append_baldeagle = " radeon-firmware kernel-modules"

APPEND += "radeon.dpm=1"

MACHINEOVERRIDES =. "amd:amdx86:radeon:"

IMAGE_FSTYPES += "wic"
WKS_FILE ?= "${@bb.utils.contains_any("EFI_PROVIDER", "systemd-boot", "systemd-bootdisk.wks", "mkefidisk.wks", d)}"
do_image_wic[depends] += "gptfdisk-native:do_populate_sysroot mtools-native:do_populate_sysroot dosfstools-native:do_populate_sysroot"